; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "SD Mod Manager"
#define MyAppVersion "0.98.0.alpha"
#define MyAppPublisher "Aliaksei SyDr Karalenka"
#define MyAppURL "http://wforum.heroes35.net/"
#define MyAppExeName "main.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8B069D3B-BAB8-4166-ADF5-6E6AF5BD8E75}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=..\LICENSE
OutputBaseFilename=mm_setup
SetupIconFile=..\icons\main-icon.ico
Compression=lzma
SolidCompression=yes
AppCopyright={#MyAppPublisher}
ShowTasksTreeLines=True
AlwaysShowGroupOnReadyPage=True
AlwaysShowDirOnReadyPage=True

[Languages]
Name: "ru"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "en"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Release\main.exe"; DestDir: "{app}"
Source: "..\LICENSE"; DestDir: "{app}"
Source: "..\vcpkg_installed\x86-windows\x86-windows\bin\WebView2Loader.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\lng\*.json"; DestDir: "{app}\lng"
Source: "..\icons\*.png"; DestDir: "{app}\icons"
Source: "..\icons\*.svg"; DestDir: "{app}\icons"
Source: "..\data\*.*"; DestDir: "{app}\data"

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall

[Types]
Name: "Full"; Description: "FullInstallation"
Name: "Custom"; Description: "CustomInstallation"; Flags: iscustom

[Components]
Name: "sdmm"; Description: "{cm:main_files}"; Types: Full Custom; Flags: fixed
Name: "redist"; Description: "Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86)"; Types: Full

[CustomMessages]
main_files=Main program files (required)
ru.main_files=Файлы программы (обязательно)

redist_title=Installing Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86)
ru.redist_title=Установка Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86)

redist_description=Please wait while Setup installs Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86) on your computer.
ru.redist_description=Пожалуйста, подождите, идёт установка Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86).

redist_status=Installing Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86)...
ru.redist_status=Устанавливается Microsoft Visual C++ Redistributable for Visual Studio 2015-2022 (x86)...

[Code]
var
  RedistLocalPath : String;
  DownloadPage: TDownloadWizardPage;

function IsRedistInstallRequired() : boolean;
begin
  Result := WizardIsComponentSelected('redist');
end;

function InstallRedist(var NeedsRestart: boolean): String;
var
  ResultCode: Integer;
  RedistPage: TOutputProgressWizardPage;
begin
  RedistPage := CreateOutputProgressPage(CustomMessage('redist_title'), CustomMessage('redist_description'));
  RedistPage.Show;

  try
    RedistPage.SetText(CustomMessage('redist_status'), '');

    if Exec(RedistLocalPath, '/passive /norestart', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode) then
      begin
        if (ResultCode = 3010) then
          begin
            Result := CustomMessage('redist');
            NeedsRestart := true;
            RegWriteStringValue(HKEY_CURRENT_USER, 'SOFTWARE\Microsoft\Windows\CurrentVersion\RunOnce', 'InstallBootstrap', ExpandConstant('{srcexe}'));
          end
      end
    else
      begin
        //MsgBox(products[i].Title + ' install failed. Result Code: ' + IntToStr(ResultCode), mbInformation, MB_OK);
        Result := 'Install error';
      end;
  finally
    RedistPage.Hide;
  end;
end;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard();
begin
  RedistLocalPath := ExpandConstant('{tmp}{\}') + 'vc_redist.x86.exe';
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
 // DownloadPage.ShowBaseNameInsteadOfUrl := True;
end;

function PrepareToInstall(var NeedsRestart: boolean): String;
begin
  if IsRedistInstallRequired() then
    Result := installRedist(NeedsRestart);
end;

function NextButtonClick(CurPageID: Integer): boolean;
begin
  Result := true;

  if (CurPageID = wpReady) and IsRedistInstallRequired() then
  begin
    DownloadPage.Clear;
    // Use AddEx to specify a username and password
    DownloadPage.Add('https://aka.ms/vs/16/release/vc_redist.x86.exe', 'vc_redist.x86.exe', '');
    DownloadPage.Show;
    try
      try
        DownloadPage.Download; // This downloads the files to {tmp}
        Result := True;
      except
        if DownloadPage.AbortedByUser then
          Log('Aborted by user.')
        else
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end;
end;
